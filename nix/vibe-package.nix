{ rustPlatform
, lib
, pkg-config

, alsa-lib

, libGL
, libxkbcommon
, wayland

, flip

, libgbm
, vulkan-loader
, vulkan-validation-layers
, vulkan-tools
, makeWrapper
}:
let
  cargoToml = builtins.fromTOML (builtins.readFile ../vibe/Cargo.toml);
in
rustPlatform.buildRustPackage rec {
  pname = cargoToml.package.name;
  version = cargoToml.package.version;

  src = builtins.path {
    path = ../.;
  };

  nativeBuildInputs = [
    pkg-config
    makeWrapper
    flip
  ];

  buildInputs = [
    alsa-lib

    # Without wayland in library path, this warning is raised:
    # "No windowing system present. Using surfaceless platform"
    wayland

    libGL
    libxkbcommon

    # Without vulkan-loader present, wgpu won't find any adapter
    vulkan-loader
    vulkan-validation-layers
    vulkan-tools
  ];

  doCheck = false;

  postInstall = ''
    wrapProgram $out/bin/$pname --suffix LD_LIBRARY_PATH : ${builtins.toString (lib.makeLibraryPath [
      # Without wayland in library path, this warning is raised:
      # "No windowing system present. Using surfaceless platform"
      wayland
      # Without vulkan-loader present, wgpu won't find any adapter
      vulkan-loader
      libgbm
    ])}

    # Install bundled shaders to XDG data dir
    mkdir -p $out/share/vibe/shaders
    cp ${../shaders}/*.wgsl $out/share/vibe/shaders/
  '';

  LD_LIBRARY_PATH = "$LD_LIBRARY_PATH:${lib.makeLibraryPath buildInputs}";

  cargoLock.lockFile = ../Cargo.lock;

  meta = {
    description = cargoToml.package.description;
    homepage = cargoToml.package.homepage;
    license = lib.licenses.gpl3;
    mainProgram = pname;
  };
}
