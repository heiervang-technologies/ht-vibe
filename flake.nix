{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    flake-parts.url = "github:hercules-ci/flake-parts";
  };

  outputs = inputs@{ flake-parts, ... }:
    flake-parts.lib.mkFlake { inherit inputs; }
      {
        systems = [
          "x86_64-linux"
          "aarch64-linux"
        ];

        perSystem = { self', lib, system, pkgs, config, ... }: {
          _module.args.pkgs = import inputs.nixpkgs {
            inherit system;

            overlays = with inputs; [
              rust-overlay.overlays.default
            ];
          };

          packages = rec {
            default = vibe;
            vibe = pkgs.callPackage (import ./nix/vibe-package.nix) { };
          };

          devShells =
            let
              rust-toolchain = pkgs.rust-bin.fromRustupToolchainFile ./rust-toolchain.toml;
            in
            rec {
              default =
                let
                  vibe = pkgs.callPackage (import ./nix/vibe-package.nix) { };
                in
                pkgs.mkShell {
                  packages = with pkgs; [
                    cargo-flamegraph
                    cargo-release
                    git-cliff
                  ] ++ [ rust-toolchain ];

                  buildInputs = vibe.buildInputs;
                  nativeBuildInputs = vibe.nativeBuildInputs;

                  LD_LIBRARY_PATH = vibe.LD_LIBRARY_PATH;
                };
              # Run the same rendering tests without a physical GPU or display.
              ci = default.overrideAttrs (_: {
                VK_DRIVER_FILES = "${pkgs.mesa}/share/vulkan/icd.d/lvp_icd.${pkgs.stdenv.hostPlatform.parsed.cpu.name}.json";
                WGPU_BACKEND = "vulkan";
              });
            };
        };
      };
}
